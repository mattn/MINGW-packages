# Maintainer: Ray Donnelly <mingw.android@gmail.com>
# Maintainer: Martell Malone <martellmalone@gmail.com>

_realname=ag
_longname=the_silver_searcher
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.2.0
pkgrel=2
pkgdesc="The Silver Searcher: An attempt to make something better than ack, which itself is better than grep (mingw-w64)"
arch=('any')
url="https://geoff.greer.fm/ag"
license=("Apache")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-pcre"
         "${MINGW_PACKAGE_PREFIX}-xz"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs') # '!strip' 'debug')
source=(${_longname}-${pkgver}::"git+https://github.com/ggreer/the_silver_searcher"
        "0001-lang-Allow-both-extensions-and-full-filenames.patch"
        "0002-win32-Detect-Cygwin-MSYS-PTY.patch"
        "0003-options-Fix-ordering-problems-with-color.patch"
        "0004-lang-Add-autotools-ac-am-in-m4-pc.patch"
        "0005-lang-Add-to-make-Makefile-Makefile.Debug-Makefile.Re.patch"
        "0006-lang-Add-makepkg-PKGBUILD-diff-patch-in-install.patch"
        "0007-lang-Add-cmake-CMakeLists.txt-CMakeCache.txt.patch"
        "0008-win32-Use-NUL-for-git-config.patch")
sha256sums=('SKIP'
            'cab015f5f321d247b06789ab2a81274f2fadd55893b69a5bfdb8458c5d291fcd'
            '221fd93a587e58468cbb9326ef6ec49f87e7b34f0d26fd80be8a4c56b4df4acf'
            '22ef8151e3e9b8df87a8cd8ed05a51eeb51756fe729d9235854dc032c40dbbd2'
            '134ed33bf0799f33faccca316e7345465fe9d0492d98840b28d96880070c75ba'
            'de0190e531c41500250b09aa6b80406db2257dcce688c6b2476a5b3493b49267'
            '9f59a6a995caa154744427d8a95c323a03ed2660191a433002628db1dbbba242'
            '18c56bd2789022b01c9179becadcfe451fee2c13663b31225285ae0c6b1d3687'
            '5d97da8a363346ac5e952e4ea00c0d70406392a4b6de1adeac029f7814cfac77')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"/${_longname}-${pkgver}
  apply_patch_with_msg \
    0001-lang-Allow-both-extensions-and-full-filenames.patch \
    0002-win32-Detect-Cygwin-MSYS-PTY.patch \
    0003-options-Fix-ordering-problems-with-color.patch \
    0004-lang-Add-autotools-ac-am-in-m4-pc.patch \
    0005-lang-Add-to-make-Makefile-Makefile.Debug-Makefile.Re.patch \
    0006-lang-Add-makepkg-PKGBUILD-diff-patch-in-install.patch \
    0007-lang-Add-cmake-CMakeLists.txt-CMakeCache.txt.patch

  # configure.ac forces -O2, so force it to -O0 if debugging.
  if check_option "debug" "y"; then
    sed -i "s#-O2#-O0#g" configure.ac
  fi

  autoreconf -fiv
}

build() {

  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"

  "${srcdir}"/${_longname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --{build,host}=${MINGW_CHOST}

  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}"/${_longname}-${pkgver}/LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
